import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'

group = 'com.trason'
version = '1.4.3'

sourceCompatibility = "17"
targetCompatibility = "17"

// Repositories
repositories {
	mavenCentral()
	maven {
		name = "papermc-repo"
		url = "https://repo.papermc.io/repository/maven-public/"
	}
	maven { url = 'https://oss.sonatype.org/content/groups/public/' }
	maven { url = 'https://repo.skriptlang.org/releases' }

}

// Dependencies
dependencies {
	// Paper
	compileOnly("io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT")

	implementation(platform("com.github.philippheuer.events4j:events4j-bom:0.12.0"))

	// Skript
	compileOnly 'com.github.SkriptLang:Skript:2.8.0'

	// bStats
	compileOnly group: 'org.bstats', name: 'bstats-bukkit', version: '3.0.1'

	// Twitch4J
	implementation group: 'com.github.twitch4j', name: 'twitch4j', version: '1.21.0'



}




// Ensure plugin.yml has the version defined here
processResources {
	from(sourceSets.main.resources.srcDirs) {
		duplicatesStrategy DuplicatesStrategy.INCLUDE // config, logback, and plugin yml files are not dupes - https://github.com/gradle/gradle/issues/17236
		filter ReplaceTokens, tokens: [version: version]
	}
}

// Create a fat jar at: build/libs/
jar {
	from {
		duplicatesStrategy DuplicatesStrategy.INCLUDE // primarily META-INF duplicates
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
}
